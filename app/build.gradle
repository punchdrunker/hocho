apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// androidのブロックでAndroid用の設定をする
android {
    compileSdkVersion 27
    // compileに使われるandroidSDKのバージョン targetSdkVersionと揃える
    defaultConfig {
        applicationId "tokyo.punchdrunker.hocho"    // <- アプリのID Play Store、端末上で一意になる識別子
        minSdkVersion 21                            // <- サポートする最小のandroidバージョン
        targetSdkVersion 27
        // <- ターゲットのandroidバージョン このバージョンによってRuntimePermissionなど一部挙動が変わる(基本的に最新に合わせたほうが良い)
        buildToolsVersion versions.buildTools
        // <- buildに利用するツール郡の設定(Android plugin for Gradle 3.0.0以降なら設定しない場合プラグインに合わせたバージョンが自動で選択される)
        versionCode 3
        // <- アプリのバージョンコード 内部的に使われるもので基本的にユーザーの目にするものではない アプリのインストール時やGoogle Play Consoleでのリリースで使われる
        versionName "1.2.0"                           // <- ユーザーが目にするバージョン名
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // <- androidTestに使われるRunner
    }

    buildTypes {
        // リリースに使う設定
        release {
            minifyEnabled false     // <- 難読化を行うか
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // <- Proguard(難読化ツールの設定ファイルを指定)
        }
    }
    // コンパイルに使うJavaのバージョンの設定
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    // data binding用の設定
    dataBinding {
        enabled = true
    }

    // app bundleを有効にする
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    dynamicFeatures = [":gallery"]
}

// 利用するライブラリの設定
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // <- app/libs の下にある*.jar ファイルを利用する設定(デフォルトで記述されるが大抵の場合不要)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    api "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.android.support:design:$versions.supportLibrary"
    api 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation "com.android.support:cardview-v7:$versions.supportLibrary"
    implementation "com.android.support:customtabs:$versions.supportLibrary"
    // <- Kotlinでプラグインによる自動生成処理を行うための設定(kotlinを使わない場合はannotationProcessor)

    api 'com.google.android.play:core:1.2.0' // <- Dynamic Featureのロードに必要

    // network
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation("com.squareup.retrofit2:converter-simplexml:$versions.retrofit") {
        exclude module: 'xpp3'
        exclude module: 'stax-api'
    }
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2" // <- 利用するライブラリを設定する

    // coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.9.0'
    // utils
    implementation 'joda-time:joda-time:2.9.9'

    // debug
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary"

    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
