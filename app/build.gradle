apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

// androidのブロックでAndroid用の設定をする
android {
    compileSdk 36
    // compileに使われるandroidSDKのバージョン targetSdkVersionと揃える
    defaultConfig {
        applicationId "tokyo.punchdrunker.hocho"    // <- アプリのID Play Store、端末上で一意になる識別子
        minSdkVersion 24                           // <- サポートする最小のandroidバージョン
        targetSdkVersion 36
        // <- buildに利用するツール郡の設定(Android plugin for Gradle 3.0.0以降なら設定しない場合プラグインに合わせたバージョンが自動で選択される)
        versionCode 12
        // <- アプリのバージョンコード 内部的に使われるもので基本的にユーザーの目にするものではない アプリのインストール時やGoogle Play Consoleでのリリースで使われる
        versionName "1.6.0"                           // <- ユーザーが目にするバージョン名
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // <- androidTestに使われるRunner
    }

    buildTypes {
        // リリースに使う設定
        release {
            minifyEnabled false     // <- 難読化を行うか
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // <- Proguard(難読化ツールの設定ファイルを指定)
        }
    }
    // コンパイルに使うJavaのバージョンの設定
    compileOptions {
        targetCompatibility JavaVersion.VERSION_21
        sourceCompatibility JavaVersion.VERSION_21
    }

    buildFeatures{
        dataBinding = true
        viewBinding true
        compose true
    }

    // composeOptions は Compose Compiler Plugin が自動的に管理します
    // Kotlin 2.0以降では kotlinCompilerExtensionVersion の明示的な指定は不要です

    // app bundleを有効にする
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    dynamicFeatures = [":gallery"]
    lint {
        abortOnError true
    }
    namespace 'tokyo.punchdrunker.hocho'
}

// 利用するライブラリの設定
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // <- app/libs の下にある*.jar ファイルを利用する設定(デフォルトで記述されるが大抵の場合不要)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.activity:activity-ktx:1.11.0"
    implementation 'androidx.browser:browser:1.9.0'
    // <- Kotlinでプラグインによる自動生成処理を行うための設定(kotlinを使わない場合はannotationProcessor)

    implementation 'com.google.android.play:feature-delivery:2.1.0' // <- Dynamic Featureのロードに必要
    implementation 'com.google.android.play:feature-delivery-ktx:2.1.0'

    // network
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation("com.squareup.retrofit2:converter-simplexml:$versions.retrofit") {
        exclude module: 'xpp3'
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'stream'
    }
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1' // <- 利用するライブラリを設定する

    // coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    // compose
    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation "androidx.compose.material3:material3:1.4.0"
    implementation "androidx.compose.animation:animation:$versions.compose"
    implementation "androidx.compose.ui:ui-tooling:$versions.compose"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.4'
    implementation "com.google.android.material:compose-theme-adapter:1.2.1"

    // utils
    implementation 'joda-time:joda-time:2.14.0'

    // debug
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary"

    // test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
    implementation 'androidx.test.ext:junit-ktx:1.3.0'
}
